plugins {
    id 'java'
    id 'de.undercouch.download' version '4.0.4'
}

def swaggerVersion = "1.3.5",
    junitVersion = "5.7.1",
    restAssuredVersion = "4.3.3"

ext {
    coverageDownloadUrl = "https://dl.bintray.com/viclovsky/maven/com/github/viclovsky/swagger/coverage/swagger-coverage-commandline/${swaggerVersion}/swagger-coverage-commandline-${swaggerVersion}.zip"
    coverageCommandline = "./.coverage/swagger-coverage-commandline-${swaggerVersion}/bin/swagger-coverage-commandline"
    swaggerDownloadUrl = "https://petstore.swagger.io/v2/swagger.json"

    coverageConfiguration = './coverage/configuration.json'
    coverageOutput = './swagger-coverage-output'
    coverageSwagger = './build/swagger.json'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://dl.bintray.com/viclovsky/maven/' }
}

task downloadSwagger(type: Download) {
    src "${swaggerDownloadUrl}"
    dest "${coverageSwagger}"
}

task downloadCommandline(type: Download) {
    src "${coverageDownloadUrl}"
    dest file(".coverage/commandline.zip")
    doFirst {
        mkdir file(".coverage")
    }
}

task unzipCommandline(type: Copy) {
    dependsOn("downloadCommandline")
    def zipFile = file(".coverage/commandline.zip")
    from zipTree(zipFile)
    into file(".coverage")
}

task coverageReport(type: Exec) {
    dependsOn('downloadSwagger', 'unzipCommandline')

    commandLine "${coverageCommandline}", "-s", "${coverageSwagger}", "-i", "${coverageOutput}", '-c', "${coverageConfiguration}"
    workingDir '.'
}

test {
    useJUnitPlatform()
}

dependencies {
    testImplementation("io.rest-assured:rest-assured:$restAssuredVersion")
    testImplementation("com.github.viclovsky.swagger.coverage:swagger-coverage-rest-assured:$swaggerVersion")
    testImplementation("com.fasterxml.jackson.core:jackson-databind:2.11.0")

    testImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
}